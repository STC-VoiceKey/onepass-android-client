apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'

def buildVersion = "$System.env.CORE_VERSION"
version = buildVersion
group = 'com.speechpro.onepass'


android {

    defaultPublishConfig "debug"

    dexOptions {
        jumboMode = true
    }

    compileSdkVersion Integer.parseInt(project.compileSdkVersion)
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        minSdkVersion Integer.parseInt(project.minSdkVersion)
        targetSdkVersion Integer.parseInt(project.targetSdkVersion)
        versionName project.versionName
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        'androidManifestFile': '$project.projectDir/src/main/AndroidManifest.xml',
                        'resourcePackageName': 'com.speechpro.onepass.core',
                        'trace'              : 'true',
                        'logLevel'           : 'TRACE',
                        'logAppenderConsole' : 'true'
                ]
            }
        }

    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    android.libraryVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            def fileName = outputFile.name
            if (outputFile != null && fileName.endsWith('.aar')) {
                if (fileName.contains('-debug.aar')) {
                    fileName = outputFile.name.replace('-debug.aar', "_debug-${buildVersion}.aar")
                    outputFileName = fileName
                } else if (fileName.contains('-release.aar')) {
                    fileName = outputFile.name.replace('-release.aar', "-${buildVersion}.aar")
                    outputFileName = fileName
                }
            }
        }

    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom.packaging = 'aar'
            artifact "build/outputs/aar/core-${buildVersion}.aar"
        }
    }
}

