buildscript {
    repositories {
        jcenter {
            url "http://jcenter.bintray.com/"
        }
    }
    dependencies {
        classpath System.properties['gradlePlugin']
        classpath System.properties['androidApt']
    }
}

apply plugin: 'com.android.library'
apply plugin: 'android-apt'
apply plugin: 'maven'
apply plugin: 'maven-publish'

def buildVersion ="$System.env.CORE_VERSION"
version = buildVersion
group = 'com.speechpro.onepass'


android {

    defaultPublishConfig "debug"

    dexOptions {
        jumboMode = true
    }

    compileSdkVersion Integer.parseInt(project.compileSdkVersion)
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        minSdkVersion Integer.parseInt(project.minSdkVersion)
        targetSdkVersion Integer.parseInt(project.targetSdkVersion)
        versionName project.versionName
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    android.libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            def fileName
            if (outputFile != null && (fileName = outputFile.name).endsWith('.aar')) {
                if (fileName.contains('-debug.aar')) {
                    fileName = outputFile.name.replace('-debug.aar', "_debug-${buildVersion}.aar")
                    output.outputFile = new File(outputFile.parent, fileName)
                } else if(fileName.contains('-release.aar')){
                    fileName = outputFile.name.replace('-release.aar', "-${buildVersion}.aar")
                    output.outputFile  = new File(outputFile.parent, fileName)
                }
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom.packaging = 'aar'

            artifact "build/outputs/aar/rest-${buildVersion}.aar"
        }
    }
}

dependencies {
    compile lib.guava
    compile lib.core
    compile lib.retrofit
    compile lib.okhttp
    compile lib.jackson

    testCompile lib.junit
}

apt {
    arguments {
        androidManifestFile "$project.projectDir/src/main/AndroidManifest.xml"
        resourcePackageName 'com.speechpro.onepass.core.rest'
        trace true
        logLevel 'TRACE'
        logAppenderConsole true
    }
}
