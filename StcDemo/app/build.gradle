apply plugin: 'com.android.application'


repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

android {
    def globalConfiguration = rootProject.ext

    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

    defaultConfig {
        applicationId "com.speechpro.stcdemo"
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
        versionCode globalConfiguration.getAt("androidVersionCode")
        versionName globalConfiguration.getAt("androidVersionName")
        multiDexEnabled true
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        release {
            storeFile file('keystore/stcdemo.jks')
            storePassword '123456As'
            keyAlias 'stcdemo'
            keyPassword '123456As'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    def separator = "-"
                    def name = "stcdemo"
                    def coreVersion = "$System.env.CORE_VERSION"
                    def fileName = "${name}${separator}${coreVersion}.apk"
                    outputFileName = new File("./../", fileName)
                }
            }
        }
        debug {
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    sourceSets {
        androidTest.resources.srcDirs = ['src/res']
    }
}

dependencies {
    def dep = rootProject.ext.dep

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':framework')
    implementation dep.multidex
    implementation dep.appcompat
    implementation dep.supportV4
    implementation dep.design
    implementation dep.cardView
    implementation dep.jackson
    implementation dep.javaxAnnotation

    annotationProcessor dep.daggerCompiler
    implementation dep.dagger

    implementation dep.butterknife
    annotationProcessor dep.butterknifeCompiler

    testImplementation 'junit:junit:4.12'
    androidTestImplementation dep.uiautomator
    androidTestImplementation dep.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}
